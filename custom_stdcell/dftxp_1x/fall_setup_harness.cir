Test Harness  for sky130_fd_sc_hd__dfxtp_4
.include 'sky130nm.lib' 
.include 'custom_stdcell/dftxp_1x/sky130_vsddfxtp_1.spice'
    .options savecurrents
    X1 CLK D VGND VNB VPB VPWR Q sky130_fd_sc_hd__dfxtp_4
    * Power Supplies
    VVPWR VPWR 0 DC 1.8
VVPB VPB 0 DC 1.8
VVGND VGND 0 DC 0
VVNB VNB 0 DC 0
    * Signal Supplies
    VD D 0 PULSE(0 1.8 6n 0.01n 0.01n 10ns 20ns)
Vclk clk 0 PULSE(0 1.8 0 0.01n 0.01n 15ns 30ns)
    * CLoad
    CLoad Q 0 1f
    * Control Generations
    .control
    let verbose = 0
            shell rm custom_stdcell/dftxp_1x/data/D/setup/fall_setup.txt 
    foreach in_transition 0.01n 0.5n 1.5n
    echo "d_tran:$in_transition" >> custom_stdcell/dftxp_1x/data/D/setup/fall_setup.txt
    foreach clk_tran 0.01n 0.5n 1.5n
        let d_base_t = 6n
        let d_high_t = d_base_t + $in_transition*1.666 
        
        if $in_transition = $clk_tran
            let off_time = 0.4n
        else
            let  off_time = 0.0
        end
        let clk_base_t = d_base_t + $in_transition*1.666 + off_time
        let clk_high_t = clk_base_t + $clk_tran*1.666 
        let lower_t = -4n
        let upper_t = 5n
        let mid_point = (lower_t + upper_t)/2
        let initial = 0
        let runs = 20
        let tpd_ref = 0
        dowhile initial < runs
            * Reference Simulation
            if initial = 0
                let mid_point = -2n
            end 
            let d_base_t_prime = d_base_t + mid_point
            let d_high_t_prime = d_base_t_prime + $in_transition*1.666 
            alter @VD[PWL] = [ 0 1.8 $&d_base_t_prime 1.8 $&d_high_t_prime 0 ]
            alter @Vclk[PWL] = [ 0 0 0.2n 1.8 1.5n 0 $&clk_base_t 0 $&clk_high_t 1.8 ]
            tran 0.005n 20n
            run
            reset
            * plot q d clk
            let tpd = 0
            * measurement commands for Tsetup and Propagation Delay(CLK-Q)
            meas tran tsetup trig v(D) val=0.9 FALL=1 targ v(clk) val=0.9 RISE=2
            meas tran tpd trig v(clk) val=0.9 RISE=2 targ v(Q) val=0.9 FALL=1
            meas tran tpd trig v(clk) val=0.9 RISE=2 targ v(Q) val=0.9 FALL=2
            * Reference Simulation 
            if initial = 0
                let tpd_ref = tpd
                let tpd_ref_dis = tpd_ref/1n
                let mid_point = (lower_t + upper_t)/2
                if verbose = 0
                echo "tpd_ref: $&tpd_ref_dis" >> custom_stdcell/dftxp_1x/data/D/setup/fall_setup.txt
                end
            else
                if verbose = 1
                    echo "Iteration: $&initial" >> custom_stdcell/dftxp_1x/data/D/setup/fall_setup.txt
                    echo "in_Upper_Bound:$&upper_t:in_Lower_Bound:$&lower_t" >> custom_stdcell/dftxp_1x/data/D/setup/fall_setup.txt
                end
            * Changing the upper and lower values as per the results
                let tpd_goal = 1.1*tpd_ref
                let error = (tpd-tpd_goal)/tpd_goal
                if tpd = 0
                    let upper_t = mid_point
                else 
                    if tpd_goal > tpd
                        let lower_t = mid_point
                    end
                    if tpd_goal < tpd
                        let upper_t = mid_point
                    end
                end
                * To Stop the loop if error is low
                if error > 0 
                    if error < 0.07m
                        let initial = 1000
                    end
                end
            end
            let initial = initial + 1
            let mid_point = (lower_t + upper_t)/2
            let tpd_num=tpd/1n
            let tsetup_num=tsetup/1n
            if verbose = 1
                echo "final_Upper_Bound:$&upper_t:final_Upper_Bound_Lower_Bound:$&lower_t" >> custom_stdcell/dftxp_1x/data/D/setup/fall_setup.txt
                echo "clk_tran:$clk_tran:pdelay_clkout:$&tpd_num:setup_time:$&tsetup_num:error_pdelay:$&error" >> custom_stdcell/dftxp_1x/data/D/setup/fall_setup.txt
            end 
        end
        if verbose = 0
            echo "clk_tran:$clk_tran:pdelay_clkout:$&tpd_num:setup_time:$&tsetup_num:error_pdelay:$&error" >> custom_stdcell/dftxp_1x/data/D/setup/fall_setup.txt
        end
    end
    end    
    quit
    rusage
    quit
    .endc