* NGSPICE file created from or2_0.ext - technology: sky130A

.include "sky130nm.lib"
.include "./or2_custom/or2_0/sky130_fd_sc_hd__or2_0.spice"

XOR1 IN1 IN2 VGND VNB VPB VPWR OUT sky130_fd_sc_hd__or2_0
v1 VPWR VGND 1.8
v11 VPB VGND 1.8
v2 IN1 VGND PULSE 0 1.8 0 0.01n 0.01n 50ns 100ns 
v3 IN2 VGND 0 
V4 VGND 0 0 
v5 VGND VNB 0
c1 out 0 0.00709641p

.control
* Input Vector - Input Delay Time(index1)
foreach in_delay 0.01n 0.0230506n 0.0531329n 0.122474n 0.282311n 0.650743n 1.5n
    
    * Initiating Text Files in folder text_files
    echo "input_delay:$in_delay" >> text_files/input_delay.txt
    echo "input_delay:$in_delay" >> text_files/cell_fall.txt
    echo "input_delay:$in_delay" >> text_files/cell_rise.txt
    echo "input_delay:$in_delay" >> text_files/fall_transition.txt
    echo "input_delay:$in_delay" >> text_files/rise_transition.txt

    * 1.666 to match the slew rate
    let actual_rtime = $in_delay*1.666
    
    * Changing the V2 Supply Rise time as per the Input Rise Time vector
    alter @v2[pulse] = [ 0 1.8 0 $&actual_rtime $&actual_rtime 50ns 100ns ]
    
    * Input Vector - Load Cap values(index2)
    foreach out_cap 0.0005p 0.00121058p 0.002931p 0.00709641p 0.0171815p 0.0415991p 0.100718p
    
        * Changing the C1 value as per the foreach list
        alter c1 $out_cap
        
        tran 1n 100ns
        run

        * Measuring Cell Fall Time @ 50% of VDD(1.8V) 
        meas tran tinfall when v(in1)=0.9 FALL=1 
        meas tran tofall when v(out)=0.9 FALL=1
        let cfall = (tofall-tinfall)/1e-9
        echo "out_cap:$out_cap:cell_fall:$&cfall" >> text_files/cell_fall.txt

        * Measuring Cell Rise Time @ 50% of VDD(1.8V) 
        meas tran tinrise when v(in1)=0.9 RISE=1 
        meas tran torise when v(out)=0.9 RISE=1
        let crise = (torise-tinrise)/1e-9
        echo "out_cap:$out_cap:cell_rise:$&crise" >> text_files/cell_rise.txt

        * Measuring Fall Transion Time @ 80-20% of VDD(1.8V) 
        meas tran ft1 when v(out)=1.44 FALL=1 
        meas tran ft2 when v(out)=0.36 FALL=1
        let fall_tran = (ft2-ft1)/1e-9
        echo "out_cap:$out_cap:fall_transition:$&fall_tran" >> text_files/fall_transition.txt
        
        * Measuring Rise Transion Time @ 20-80% of VDD(1.8V) 
        meas tran rt1 when v(out)=1.44 RISE=1 
        meas tran rt2 when v(out)=0.36 RISE=1
        let rise_tran = (rt1-rt2)/1e-9
        echo "out_cap:$out_cap:rise_transition:$&rise_tran" >> text_files/rise_transition.txt

    end
    
    * Verification of INPUT RISE TIME
    meas tran ts1 when v(in1)=1.44 RISE=1 
    meas tran ts2 when v(in1)=0.36 RISE=1
    meas tran ts3 when v(in1)=1.44 FALL=1 
    meas tran ts4 when v(in1)=0.36 FALL=1
    let RISE_IN_SLEW = (ts1-ts2)/1e-9
    let FALL_IN_SLEW = (ts4-ts3)/1e-9
    echo "actual_rise_slew:$&RISE_IN_SLEW" >> text_files/input_delay.txt
    echo "actual_fall_slew:$&FALL_IN_SLEW" >> text_files/input_delay.txt

end  

* plot in1 in2 out

.endc

